"Start Symbol" = <block>

<block>::= <operation> <block> | <operation>

<operation>::= <attribute> | <declaration> | <if> | <while> | <jumppoint> | <goto>

<operand>::= 'ID' | 'number' | '-' 'number' | 'letter'
<cmpexp>::= <boolexp> <cmpexp1> | <boolexp>
<cmpexp1>::= '==' <boolexp> | '!=' <boolexp> | '<=' <boolexp> | '>=' <boolexp> | '<' <boolexp> | '>' <boolexp>
<boolexp>::= '!' <addexp> | <addexp>
<addexp>::= <addexp> <addexp1> | <mulexp>
<addexp1>::= '+' <mulexp> | '-' <mulexp>
<mulexp>::= <mulexp> <mulexp1> | <termexp>
<mulexp1>::= '*' <termexp> | '/' <termexp>
<termexp>::= <operand> | '(' <expression> ')'
<expression>::= <cmpexp>

<assignment>::= '=' <expression> ';'
<attribute>::= 'ID' <assignment>

<type>::= 'int' | 'char'
<declaration>::= <type> 'ID' <declaration1>
<declaration1>::= <assignment> | ';'

<while>::= 'while' '(' <expression> ')' '{' <block> '}'
<if>::= 'if' '(' <expression> ')' '{' <block> '}' | 'if' '(' <expression> ')' '{' <block> '}' 'else' <else>
<else>::= <if> | '{' <block> '}'

<jumppoint>::= 'ID' ':'
<goto>::= 'goto' 'ID' ';'
